buildscript {
    ext {
        kotlinVersion = '1.3.50'
        jacksonVersion = '2.9.9'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlinVersion
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-allopen', version: kotlinVersion
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-noarg', version: kotlinVersion

        classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.12.0'
        classpath group: 'io.codearte.gradle.nexus', name: 'gradle-nexus-staging-plugin', version: '0.21.2'
        classpath group: 'com.github.ben-manes', name: 'gradle-versions-plugin', version: '0.17.0'
        classpath group: 'org.jmailen.gradle', name: 'kotlinter-gradle', version: '2.3.2'
        classpath group: 'de.marcphilipp.gradle', name: 'nexus-publish-plugin', version: '0.4.0'
        classpath group: 'org.jetbrains.dokka', name: 'dokka-gradle-plugin', version: '0.10.1'
    }
}

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.jmailen.kotlinter'
apply plugin: 'de.marcphilipp.nexus-publish'
apply plugin: 'org.jetbrains.dokka'

scmVersion {
    tag {
        prefix = 'chrome-reactive-kotlin'
    }
}

group = 'pl.wendigo'
version = scmVersion.version

jar {
    baseName = 'chrome-reactive-kotlin'
    version = scmVersion.version
}

compileKotlin {
    kotlinOptions {
        languageVersion = "1.3"
        jvmTarget = "1.8"
        verbose = false
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion

    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.6.0'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.19'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile group: 'org.testcontainers', name: 'testcontainers', version: '1.14.1'
    testCompile group: 'org.testcontainers', name: 'spock', version: '1.14.1'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'html'
    outputDirectory = javadoc.destinationDir

    configuration {
        jdkVersion = 8
        includes = ['package.md', 'domains.md']
        samples = ['samples/basic.kt']
        reportUndocumented = true

        sourceLink {
            path = "src/main/kotlin"
            url = "https://github.com/wendigo/chrome-reactive-kotlin/tree/master/src/main/kotlin"
            lineSuffix = "#L"
        }
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'chrome-reactive-kotlin'
                description = 'Chrome Reactive - low level, remote chrome debugger protocol client (DevTools)'
                url = 'https://github.com/wendigo/chrome-reactive-kotlin'
                inceptionYear = '2017'
                version = version
                groupId = group
                artifactId = 'chrome-reactive-kotlin'
                packaging = 'jar'

                scm {
                    connection = 'scm:git:https://github.com/wendigo/chrome-reactive-kotlin.git'
                    url = 'https://github.com/wendigo/chrome-reactive-kotlin'
                    developerConnection = 'scm:git:https://github.com/wendigo/chrome-reactive-kotlin.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'Mateusz "Serafin" Gajewski'
                        email = 'contact@wendigo.pl'
                    }
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

nexusStaging {
    packageGroup = 'pl.wendigo'
    numberOfRetries = 3
    delayBetweenRetriesInMillis = 100000
    username = nexusUsername
    password = nexusPassword
}

wrapper {
    version = '5.6.2'
    distributionType = Wrapper.DistributionType.ALL
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        }
    }
}

kotlinter {
    ignoreFailures = false
    indentSize = 4
    continuationIndentSize = 4
    reporters = ["checkstyle", "plain"]
    experimentalRules = true
    disabledRules = []
    fileBatchSize = 30
}

nexusPublishing {
    repositories {
        sonatype()
    }
}
